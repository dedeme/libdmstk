// Copyright 25-Sep-2019 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

300 klen =
/// Int
/// Standard keys length.

2592000 tNoExpiration. = // seconds == 30 days

"nkXliX8lg2kTuQSS/OoLXCk8eS4Fwmc+N7l6TTNgzM1vdKewO0cjok51vcdl"
"OKVXyPu83xYhX6mDeDyzapxL3dIZuzwyemVw+uCNCZ01WDw82oninzp88Hef" +
"bn3pPnSMqEaP2bOdX+8yEe6sGkc3IO3e38+CqSOyDBxHCqfrZT2Sqn6SHWhR" +
"KqpJp4K96QqtVjmXwhVcST9l+u1XUPL6K9HQfEEGMGcToMGUrzNQxCzlg2g+" +
"Hg55i7iiKbA0ogENhEIFjMG+wmFDNzgjvDnNYOaPTQ7l4C8aaPsEfl3sugiw" + dmKey. =

{} map,from Cgi. =
/*
  fkey: String     // key to encrypt files
  key: Opt<String> // key to communications.
  home: String;
  tExpiration: Int
*/

( tExpiration =; home =

  this,Cgi.
    "fkey"        : this,dmKey. this,dmKey. size cryp,key : map,put+
    "key"         : wrap,none : map,put+
    "home"        : home : map,put+
    "tExpiration" : tExpiration : map,put

  home file,exists? ! (home file,mkdir) if

  home "users.db" path,+ file,exists? !
  (
    [] data this,writeUsers.
    "admin" this,dmKey. "0" this,putUser.
    [] data this,writeSessions.
  )
  if
) init =>
/// (String, Int) -> Void

// Users -----------------------------------------------------------------------

( Users =  // (List<String>) -> Void
  this,Cgi. .home : "users.db" : path,+
    Users (js,ws) lst,map js,wa : this,Cgi. .fkey : cryp,cryp
      file,write
) writeUsers. =>

( // () -> List<String>
  this,Cgi. .home : "users.db" : path,+
  file,read
    this,Cgi. .fkey cryp,decryp
    js,ra
    (js,rs) lst,map
) readUsers. =>

( id =; Users =  // (List<String>, String) -> Void
  Users (u =; u ":" str,split 0 get id ==) lst,index : ix =
  (Users ix lst,remove) (ix -1 !=) if
) removeUser. =>

( level =; key =; id =  // (String, String, String) -> Void
  this,readUsers. Users =
  Users id this,removeUser.
  Users
    id : ":" : key this,klen cryp,key : ":" : level + + + + lst,push+
    this,writeUsers.
) putUser. =>

( id =   // (String) -> Void
  this,readUsers.
  dup id this,removeUser.
  this,writeUsers.
) delUser. =>

( key =; id =   // (String, String) -> Opt<String>  Returns user level
  key this,klen cryp,key : kkey =
  this,readUsers. : Users =

  Users (":" str,split 0 get id ==) lst,find
  ( ":" str,split Fields =;
    Fields 1 get kkey == (Fields 2 get wrap,some) (wrap,none) elif
  )
  ( wrap,none)
  wrap,option
) checkUser. =>

( level =; id =   // (String, String) -> Int
  [0] data : Changed =
  this,readUsers.
    ( u =;
      u ":" str,split : Ds =;

      Ds 0 get id ==
      (
        Changed 1 <<
        Ds 0 get : ":" : Ds 1 get : ":" : level + + + +
      )
      ( u)
      elif
    )
    lst,map this,writeUsers.
  Changed >>
) changeLevel. =>

( newPass =; oldPass =; id =  // (String, String, String) -> Int
  newPass this,klen cryp,key : newPassK =
  oldPass this,klen cryp,key : oldPassK =

  0 wrap,ref : Changed =
  this,readUsers.
    ( u =;
      u ":" str,split : Ds =;

      Ds 0 get id == : Ds 1 get oldPassK == : &&
      (
        Changed 1 <<
        Ds 0 get : ":" : newPassK : ":" : Ds 2 get : + + + +
      )
      ( u)
      elif
    )
    lst,map this,writeUsers.
  Changed >>
) changePass. =>

// Sessions --------------------------------------------------------------------

( Sessions =  // (List<Js>) -> Void  . Js is a JSON array
  this,Cgi. .home : "sessions.db" : path,+
    Sessions js,wa : this,Cgi. .fkey : cryp,cryp
  file,write
) writeSessions. =>

(   // () -> List<Js>. Js is a JSON array
  this,Cgi. .home : "sessions.db" : path,+
  file,read : this,Cgi. .fkey : cryp,decryp : js,ra
) readSessions. =>

( expiration =; comKey =; id =; sessionId =
    // (String, String, String, Int) -> Void
    // If expiration is 0 tNoExpiration is used
  expiration (expiration) (this,tNoExpiration.) elif : lapse =
  time,now lapse time,add : tm =

  this,readSessions.
    [
      sessionId js,ws // 0
      user js,ws      // 1
      comKey js,ws    // 2
      "" js,ws        // 3 conId
      tm js,wi        // 4
      lapse js,wi     // 5
    ] data js,wa lst,push+
    this,writeSessions.
) addSession. =>

( sessionId =   // (String) -> Void
  this,readSessions.
    (js,ra 0 get js,rs : sessionId : !=) lst,filter this,writeSessions.
) delSession. =>

( conId =; sessionId =   // (String, String) -> Void
  this,readSessions.
    ( SsJs =
      SsJs js,ra Ss =

      Ss 0 get js,rs : sessionId : ==
      ( Ss : 3 : conId js,ws : set+; js,wa)
      ( SsJs)
      elif
    ) lst,map this,writeSessions.
) setConnectionId. =>

// Public interface ------------------------------------------------------------

( this,Cgi. .home)
home =>
/// () -> String
/// Returns 'home' directory.

( k =;
  this,Cgi. : "key" : k wrap,some : map,put
) setKey =>
/// (String) -> Void
/// Sets the key which 'cgi ok' and 'cgi err' will use. This function is called
/// when connection or authentication.

( sessionId =
  sessionId this,klen cryp,genk this,setConnectionId.
  sessionId this,getSession
  ( )
  (
    {
      "key": "" js,ws
      "connectionId": "" js,ws
    } map,from
  )
  wrap,option
  this,ok
) connect =>
/// (String) -> String
/// Returns client 'connectionId' and 'key'. If conection failed both are "".
///   session_id: Session identifier.
///   return    : JSON: {connectionId: String, key: String}.
///               'key' is a new key, set for the new connection.

( expiration =; key =; user =
  user key this,checkUser.
  ( level =
    this,klen cryp,genk : sessionId =
    this,klen cryp,genk : key =
    sessionId : user : key
      expiration (this,Cgi. .tExpiration) (0) elif
      this,addSession.
    {
      "level": level js,ws
      "sessionId": sessionId js,ws
      "key": key js,ws
    } map,from
  )
  (
    {
      "level": "" js,ws
      "sessionId": "" js,ws
      "key": "" js,ws
    } map,from
  )
  wrap,option
  this,ok
) authentication =>
/// (String, String, Int) -> String
/// Sends to client level, key, page_id and session_id.
/// If authentication failed every value is "".
///   user      : User name.
///   key       : User password.
///   expiration: If is set to 0, session will expire after 30 days.
///   return    : JSON: {level: String, key: String, sessionId: String}

( sessionId =
  time,now : now =

  this,readSessions.
    (js,ra 4 get js,ri : now : >=) lst,filter
    ( SsJs =
      SsJs js,ra Ss =

      Ss 0 get js,rs : sessionId : ==
      (
        Ss 5 get : js,ri : lapse =
        Ss : 4 : time,now lapse time,add js,wi : set+
        js,wa
      )
      ( SsJs)
      elif
    ) lst,map : Sessions =

  Sessions this,writeSessions.

  Sessions (js,ra 0 get js,rs : sessionId : ==) lst,find
  ( Ss =
    {
      "key": Ss js,ra 2 get
      "connectionId": Ss js,ra 3 get
    } map,from wrap,some
  )
  ( wrap,none)
  wrap,option
) getSession =>
/// String -> Opt<Obj {key: Js, conId: Js}>
///   sesionId: Session identifier.
///   return  : Option with communication key and connection identifier.

( sessionId =
  this,readSessions.
    (js,ra 0 get js,rs : sessionId : !=) lst,filter
    this,writeSessions.
    this,none
) delSession =>
/// String -> JSON: {}
/// Deletes 'sessionId' and returns an empty response.

( level =; ukey =; user =; akey =; admin =
  {} "ok"
    admin akey checkUser.
    ( "0" == (user ukey level putUser.; 1) (0) elif)
    ( 0)
    wrap,option
    js.wb map,put+ this,ok
) addUser =>

( user =; akey =; admin =
  {} "ok"
    admin akey checkUser.
    ( "0" == (user delUser.; 1) (0) elif)
    ( 0)
    wrap,option
    js.wb map,put+ this,ok
) delUser =>

( level =;user =; akey =; admin =
  {} "ok"
    admin akey checkUser.
    ( "0" == (user level changeLevel.; 1) (0) elif)
    ( 0)
    wrap,option
    js.wb map,put+ this,ok
) changeLevel =>

( @sss this,changePass. r =; {} map,from "ok" r js,wb map,put+ this,ok @s)
changePass =>
/// (String, String, String) -> JSON: {ok: Boolean}
/// Changes user password.
///   user   : User name to change password.
///   key    : Old password.
///   new_key: New password.
///   return : JSON: {ok:true|false}, sets to true if operation
///            succeeded. A fail can come up if 'user' authentication fails.

( @m
  js,wo
    this,Cgi. .key
    ( cryp,cryp)
    ( "Cgi has not been initialized")
    wrap,option
@s) ok =>
/// (Map<JSON>) -> String

(
  {} map,from this,ok
@s) none =>
/// () -> String

( @s:: msg =
  { "error": msg js,ws } map,from this,ok
@s) error =>
/// (String) -> String

(
  { "expired": 1 js,wb } map,from this,ok
@s) expired =>
/// () -> String
